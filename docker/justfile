export DOCKER_USERID := `id -u`
export DOCKER_GROUPID := `id -g`

# Load .env files by default
set dotenv-load := true

# enable modern docker build features
export DOCKER_BUILDKIT := "1"
export COMPOSE_DOCKER_CLI_BUILD := "1"

export BIN := "/opt/venv/bin"

export DEV_USERID := `id -u`
export DEV_GROUPID := `id -g`


build env="dev":
    #!/usr/bin/env bash

    # set build args for prod builds
    export BUILD_DATE=$(date -u +'%y-%m-%dT%H:%M:%SZ')
    export GITREF=$(git rev-parse --short HEAD)

    # build the thing
    docker-compose build --pull {{ env }}


# run JS linter
check-js:
    docker-compose run --rm node-assets /app/docker/entrypoints/check-js.sh


# run python checks
check-py: build
    docker-compose run --rm dev /app/docker/entrypoints/check.sh


# run tests in docker container
test-py *args="":
    docker-compose run --rm test python -m pytest \
    --cov=builder \
    --cov=codelists \
    --cov=coding_systems \
    --cov=mappings \
    --cov=opencodelists \
    --cov-report html \
    --cov-report term-missing:skip-covered {{ args }}

test-js:
    docker-compose run --rm node-assets npm run test

test: test-js test-py

# run dev server in docker container
serve env="dev" *args="":
    #!/usr/bin/env bash
    if [ "$env" = "prod" ]; then
        echo foo
        SCRIPT_DIR=$( cd -- "$( dirname -- "${BASH_SOURCE[0]}" )" &> /dev/null && pwd )
        mkdir "$SCRIPT_DIR/storage"
        sudo chown 10003:10003 "$SCRIPT_DIR/storage"
    fi
    docker-compose up {{ args }} {{ env }}


# run command in dev container
run *args="bash":
    docker-compose run dev {{ args }}


# exec command in existing dev container
exec *args="bash":
    docker-compose exec dev {{ args }}


# run a basic functional smoke test against a running opencodelists
smoke-test host="http://localhost:7000":
    #!/bin/bash
    set -eu
    curl -I {{ host }} -s --compressed --fail --retry 20 --retry-delay 1 --retry-all-errors
